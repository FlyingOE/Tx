.module.tsalgolib:2019.08.21;
\l qml.q
//算法辅助函数
.enum.TextParaMap:`st`et`pr`ds`rp`ts`sf`df`cs`ct`co`ma`fd`cd`tq`jj`rn`rq`rt`ga`li`ls`mb`ms`hf`ss`si!`StartTime`EndTime`ParticipationRate`DisplaySize`ReferencePrice`TradingStyle`SliceFreq`DiceFreq`ChildStyle`CxlTimeout`CutOffset`MinAmt`NewSec`CxlSec`CutSec`BidLevel`DiceRandN`DiceRandQ`DiceRandT`GoAhead`ListID`ListSize`MaxBuy`MaxSell`HedgeFreq`SpreadSup`SpreadInf; /算法参数缩写描述

.enum.BidLevelMap:`e`d`c`b`a`0`1`2`3`4`5!`MOSTPASSIVE`MOREPASSIVE`PASSIVE`LESSPASSIVE`LEASTPASSIVE`NEUTRAL`LEASTAGGRESSIVE`LESSAGGRESSIVE`AGGRESSIVE`MOREAGGRESSIVE`MOSTAGGRESSIVE; /子单报盘策略缩写

applyparatx:{[x;y]z:{(k^.enum.TextParaMap k:x[0])!x[1]} "S=," 0: y;if[0<count u:z`id;if[0=@[{type value x};u;0];:"WrongAlgo"];.db.O1[x;`algo]:`$u;z:`id _z];if[count (key z) except (value .enum.TextParaMap);:"WrongPara"];if[0<count ts:z`StartTime;if[null "Z"$ts;z[`StartTime]:string .z.D+"T"$ts]];if[0<count ts:z`EndTime;if[null "Z"$ts;z[`EndTime]:string .z.D+"T"$ts]];if[0<v:"I"$z`NewSec;if[0<>mod[60;v];:"WrongSpeed"];z[`SliceFreq`DiceFreq]:("1";string 60%v)];if[0<v:"I"$z`CxlSec;z[`CxlTimeout]:string 00:00:01*v];if[0<count u:z`CutSec;z[`CutOffset]:string 00:00:01*v:"I"$u];if[not null v:`$z`BidLevel;if[not v in key .enum.BidLevelMap;:"WrongLevel"];z[`ChildStyle]:string .enum.BidLevelMap v];.db.O1[x;`para]:enlist (`Text _.db.O1[x;`para][0]),z;()};

intradayvol:{[x;y;z]if[0>=r:.ctrl.conn.rdb.h;:0n];r ({[x;y;z](exec last cumqty from quote where (`time$time)<=z,sym=x)-0f^exec last cumqty from quote where (`time$time)<=y,sym=x};x;y;z)}; //[fsym;starttime;endtime]取当日区间成交量

intradayvolpx:{[x;y;z;u;v]if[0>=r:.ctrl.conn.rdb.h;:0n];d:update q:deltas q from r({[x;y]select t:`time$time,q:cumqty,p:price from quote where (`time$time) within y,sym=x};x;(y,z));$[u="1";exec sum q from d where p<=v;exec sum q from d where p>=v]}; /[sym;starttime;endtime;side;px]取当日区间限价内成交量

intradayvwap:{[x;y;z]if[0>=r:.ctrl.conn.rdb.h;:0n];h:value r({exec `time$time,cumqty,vwap from quote where sym=x};x);k:where h[0] within (y,z);$[count k;((0e^prd h[1 2;last k])-(0e^prd h[1 2;-1+first k]))%((0e^h[1;last k])-(0e^h[1;-1+first k]));0n]}; /取当日区间内成交均价

dvolpred:{[x]y:.temp.HSD[x;`amt]%get_last_price[x];if[0>=r:.ctrl.conn.rdb.h;:y];fb:value exec max bucketstop,sum tr from .temp.HSVP where sym=x,bucketstop<=`time$now[];z:r({[x;y]exec last cumqty from quote where (`time$time)<=y,sym=x};x;fb[0]);$[0<z;z%fb[1];y]}; /全日成交量预测 

planadjust:{[x]if[null .db.O1[x;`amt];:()];plan:.db.O1[x;`plan];if[0<>dq:(rq:.db.O1[x;`qty]-.db.O1[x;`snap][1])-pq:sum pn:plan[1];n:count pn;i:.db.O1[x;`snap][0];pm:i _ pn;dp:(i#0f),$[i=n;();0=sm:sum pm;dq,(0|n-i+1)#0f;pm*dq%sm];pn:roundv[.db.O1[x;`sym`side]] nonneg pn+dp;plan[1]:pn;.db.O1[x;`plan]:plan];}; /金额单委托数量调整 

imadjust:{[x]if[(`VWAP<>.db.O1[x;`algo])|(0=abs c:ffill .db.Ts[.conf.algots]`IMCOEF);:()];plan:.db.O1[x;`plan];pn:plan[1];i:.db.O1[x;`snap][0];pm:i _ pn;if[1>=count pm;:()];p0:pm[0];p1:1 _ pm;s:.db.O1[x;`sym];sd:.db.O1[x;`side];ps:getordpx[s;.enum`BUY;] each `LEASTPASSIVE`LEASTAGGRESSIVE;vwap:intradayvwap[s;plan[0;0];`time$now[]];dp:p0*c*$[sd=.enum`BUY;1;-1]*$[ps[1]<vwap;1;ps[0]>vwap;-1;0];p0+:dp;p1*:1-dp%sum p1;pn:roundv[.db.O1[x;`sym`side]] nonneg (i#pn),p0,p1;plan[1]:pn;.db.O1[x;`plan]:plan;}; /IM调整:AIM(AggeessiveInTheMoney)当价格相对VWAP有利时将量提前反之延后,PIM(PassiveInTheMoney)则相反(20110701)

limitadjust:{[x]plan:.db.O1[x;`plan];if[0>=n1:(n:count pn:plan[1])-(i:.db.O1[x;`snap][0])+1;:()];vi:pn[i];if[0>=av:vi-0|("F"$string `$cfill .db.O1[x;`para][0;`MaxShow])-(.db.O1[x;`sentqty]-.db.O1[x;`cumqty]);:()];pm:(i+1) _ pn;dp:(i#0f),(neg av),$[0<sm:sum pm;pm*av%sm;n1#av%n1];pn:pn+dp;plan[1]:pn;.db.O1[x;`plan]:plan;}; /[母单号]限价单超限后计划调整,当期计划数量均摊到后续时间片上 roundv[.db.O1[x;`sym`side]] 

booksupply:{[x;y;z;w]}; /[sym;side;selfdepth peerdepth;window]limit orderbook supply 

bookrefsize:{[x;y;z]$[y=.enum`BUY;[p1:b1:`bsize;p5:b5:`bsizeQ;m1:o1:`asize;m5:o5:`asizeQ];[m1:b1:`bsize;m5:b5:`bsizeQ;p1:o1:`asize;p5:o5:`asizeQ]];t1:`time$now[];$[z=`0;.db.QX[x;p1];z=`1;.db.QX[x;m1];z=`2;.db.QX[x;b1];z=`3;.db.QX[x;o1];z=`4;sum .db.QX[x;b1,o1];z=`5;avg .db.QX[x;b5] except 0f;z=`6;avg .db.QX[x;o5] except 0f;z=`7;sum .db.QX[x;p5];z=`8;sum .db.QX[x;m5];z=`A;intradayvol[x;t1-00:01:00;t1];z=`B;intradayvol[x;t1-00:05:00;t1];sum .db.QX[x;b5,o5]]}; /[sym;side;tag]

placeahead:{[x;i]if[not null .db.Ax[x;`ahead;i];:()];k:(`$(string x),"_",(string i),"_A",(string newid[]));v:roundv[.db.O1[x;`sym`side]] .db.O1[x;`plan][1;i];s:.db.O1[x;`sym];p:first (.db.QX[s;$[.db.O1[x;`side]=.enum`BUY;`bidQ;`askQ]] except .db.QX[s;$[.db.O1[x;`side]=.enum`BUY;`bid;`ask]]) except .db.O[;`price] each value .db.Ax[x;`ahead];if[0<v&p;z:newsofixl[x;k;v;p;i];.db.Ax[x;`ahead;i]:z];}; /[upid;planidx;aheadlevel]下预埋单

getgoahead:{[x]0i^.db.Ts[.conf.algots][`GOAHEAD]^"I"$cfill .db.O1[x;`para][0;`GoAhead]}; /[upid]

goahead:{[x;i]n:count .db.O1[x;`plan][0];if[not null k:.db.Ax[x;`ahead;i];if[(0<(-/)0f^.db.O[k;`qty`cumqty])&.db.O[k;`cstatus]=.enum`NULL;cxlord k];.db.Ax[x;`ahead]_:i];placeahead[x] each .math.filter[n>] i+1+til getgoahead[x];}; /[upid;planidx]预埋单处理

/[upid]执行计划处理:对到时的子任务bin根据报价策略和流控因子生成小单进行处理(超时的小单系统会自动进行撤单再追单1次,如追单不成功则计入下一子任务)
execplan:{[x]
 if[0=n:count .db.O1[x;`plan][0];:()]; /无计划可供执行,退出
 t:`time$now[];ag:.db.O1[x;`algo];
 i:.db.O1[x;`snap][0]; /当前计划待处理bin下标
 if[(nf:i<count .db.O1[x;`plan][0])&(.db.O1[x;`plan][0;i]>t);:()]; /未到拆单时刻,退出 if[(0<i&.db.Ts[.conf.algots]`GOAHEAD)&(ag<>`VOLPCT);goahead[x;i]];
 if[(i=0)&(0<getgoahead[x])&(ag<>`VOLPCT);goahead[x;1]];
 s:.db.O1[x;`sym];sd:.db.O1[x;`side];lp:.db.O1[x;`price];if[0=0f^p:getordpx[s;sd;$[.db.O1[x;`plan][3;i] in 0N 0 0W;`MOSTAGGRESSIVE;$[ag~`VOLPCT;`LEASTAGGRESSIVE;.db.O1[x;`style]]^`$cfill .db.O1[x;`para][0;`ChildStyle]]];if[not 1b~.db.Ax[x;`AlerNulltPrice];alertoa[x;.enum`INFO;.enum`USERATTENTIONREQ;`PriceNotAvail];.db.Ax[x;`AlerNulltPrice]:1b];:()]; /取不到行情价格,告警后退出
 qtyadjust[x;p];planadjust[x];
 psp:getordpx[s;sd;`LEASTPASSIVE];if[(0<lp)&(0>=pxcmp[sd;lp;psp])&(not ag in `VOLPCT);limitadjust[x]]; /2011.09.22增加限价单超限调整 
 if[.db.O1[x;`cumqty]>.db.O1[x;`qty]-$[sd=.enum`SELL;1f;getqtymin[.db.O1[x;`sym`side]]];.db.O1[x;`end]:1b;:()];  /无单可拆,退出
 /imadjust[x]; /add AIM/PIM adjusting enhencement to gaming VWAP benchmark(20110701)
 sq:0f^.db.O1[x;`sentqty]; /母单总共已发委托
 cq:sq-.db.O1[x;`snap][1]; /母单在当前计划已发委托(.db.O1[x;`snap][1]为改单原始发单量)
 pr:$[ag in `VWAP`TWAP`VOLPCT;"F"$string `$cfill .db.O1[x;`para][0;`ParticipationRate];0f]; /参与率流控因子

 vm:$[pr<=0;0w; /无流控
 1e-2*pr*$[0=i;(.temp.HSD[s;`amt]%get_last_price[s])*.db.O1[x;`plan][2;0]; /i=0(计划未开始)时,用历史成交均量*i桶历史日内相对换手作为当日前i桶成交量预测
 	  nf;(1+.db.O1[x;`plan][2;i]% sum .db.O1[x;`plan][2;til i])*$[(0<.db.O1[x;`price])&(not "A"~cfill .db.O1[x;`para][0;`ReferenceVolume]);intradayvolpx[;;;sd;lp];intradayvol][.db.O1[x;`sym];.db.O1[x;`plan][4;0];.db.O1[x;`plan][4;i]]; /计划执行中
          $[(0<.db.O1[x;`price])&(not "A"~cfill .db.O1[x;`para][0;`ReferenceVolume]);intradayvolpx[;;;sd;.db.O1[x;`price]];intradayvol][.db.O1[x;`sym];(`time$.db.O1[x;`ntime])^`time$"Z"$cfill .db.O1[x;`para][0;`StartTime];(.conf.ex[fs2e .db.O1[x;`sym];`closePM])^`time$"Z"$cfill .db.O1[x;`para][0;`EndTime]] /        
	] /i>0时,用前i-1桶总成交量*(前i桶历史日内相对换手)%(前i-1桶历史日内相对换手)作为当日前i桶成交量预测
 ];
 v:0|$[nf; /计划尚未拆完,处理当前bin
 ((sum .db.O1[x;`plan][1;til 1+i+$[ag~`VOLPCT;0;getgoahead[x]]])&vm)-cq; /根据计划/流控/已发单量计算待发单量 /while[i+1<count .db.O1[x;`plan][0];if[.db.O1[x;`plan][0;i+1]>t;i:i+1]];
 (.db.O1[x;`qty]&vm)-sq /计划已拆完,处理已撤未补的尾单 
 ]; /完成对待下单量的计算

 
 if[(0<lp)&(0>=pxcmp[sd;lp;psp]);v&:0|("F"$string `$cfill .db.O1[x;`para][0;`MaxShow])-(.db.O1[x;`sentqty]-.db.O1[x;`cumqty])];
 if[0<cpr:"F"$string `$cfill .db.O1[x;`para][0;`MaxChildVolPct];rv:`$cfill .db.O1[x;`para][0;`RefChildVol];v&:1e-2*cpr*bookrefsize[s;sd;rv]];
 if[not null bid:.db.O1[x;`upid];if[0<ma:0f^"F"$cfill .db.O1[bid;`para][0;$[sd=.enum`BUY;`MaxBuy;`MaxSell]];v&:0f|(ma-0f^$[sd=.enum`BUY;1;-1]*.db.O1[bid;`cumamt])%p]]; /2013.05.02
 v:$[(sd=.enum`SELL)&(((-/).db.O1[x;`qty`cumqty])<getqtymin[(s;sd)]);{floor x+1e-2};roundqty[(s;sd)]] v;

 if[(v>0)&(not violateoddlotrule[x;v])&(not violateminamtrule[x;v;p]);newsox[x;(`$(string x),"_",(string i),"_",(string newid[]));v;p;i;(getqtymax[s,sd])&{$[x>0;x;0w]} 0f^"F"$string `$cfill .db.O1[x;`para][0;`MaxChildVol]]]; /下单,零股卖出除尾单外不下单(20120312)
 if[nf;.db.O1[x;`snap]:((i+1),1_.db.O1[x;`snap])]; /推进到下一计划bin
 if[(i>0)&(0<getgoahead[x])&(ag<>`VOLPCT);goahead[x;i+1]];
 }; 


/取有效时间桶列表[id;ignore_auction_flag;vpmodel]
getbuckets:{[x;y;z]ap:`$cfill x[`para][0;`ExcludeAuctions];st:"Z"$cfill x[`para][0;`StartTime];et:"Z"$cfill x[`para][0;`EndTime];s:x[`sym];if[y&0=exec count i from .temp.HSVP where sym=s;ex:fs2e s;s:$[`XSHG~ex;`600000.XSHG;`XSHE~ex;`000001.XSHE;`000300.XSHG^exec first sym from .temp.HSD where (fs2e each sym)=ex]];m:.db.Ts[.conf.algots][`SLICEFREQ]^"I"$cfill x[`para][0;`SliceFreq];api:$[(x[`algo] in ``IS)|ap=`$"1 4";();ap=`1;enlist 0W;ap=`4;enlist 0;0 0W];$[`ls=z;[d:select first bucketstart,last bucketstop,tr:sum trls by seq:1+m xbar i from select from .temp.HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:trls from .temp.HSVP where sym=s,bucket in api];`arma=z;[d:select first bucketstart,last bucketstop,tr:sum trarma by seq:1+m xbar i from select from .temp.HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:trarma from .temp.HSVP where sym=s,bucket in api];[d:select first bucketstart,last bucketstop,sum tr by seq:1+m xbar i from select from .temp.HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr from .temp.HSVP where sym=s,bucket in api]];st:$[null st;-0Wt;`time$st]|`time$x[`ntime];et:$[null et;0Wt;`time$et];d:value exec truestart,tr,truestop,seq,br from update tr:tr*br from update br:(truestop-truestart)%(bucketstop-bucketstart) from update truestart:st|bucketstart,truestop:et&bucketstop from select from `bucketstart xasc d where bucketstop>st,bucketstart<et}; /[.db.O1记录;TWAP类型标志;模型]如为上市首日新股，对TWAP/VOLPCT单历史交易分布用本市场其它股票代替(20110613)

/将1个大bucket(分钟级)打散为若干小bin(秒级)
randsymm:{$[x>0;(rand 2*x)-x;0]};
diceplan:{[upid;x;y;z]u:.db.O1[upid;`sym`side];cutoff:(last y[2])-(`time$.db.Ts[.conf.algots][`CUTOFFSET]^"T"$cfill .db.O1[upid;`para][0;`CutOffset]);m:?[{(x=0)|(x=0W)}y[3];1;1|`int$(1+ randsymm each (count y[4])#0f|1f&(ffill .db.Ts[.conf.algots]`DICERANDN)^"F"$cfill .db.O1[upid;`para][0;`DiceRandN])*(.db.Ts[.conf.algots][`DICEFREQ]^"I"$cfill .db.O1[upid;`para][0;`DiceFreq])*y[4]];x:roundv[u] (,/)x($[`EQU=z;{{y*x%sum y}[x] (y#x%y)*(1+randsymm each y#0f|1f&(ffill .db.Ts[.conf.algots]`DICERANDQ)^"F"$cfill .db.O1[z;`para][0;`DiceRandQ])}[;;upid];{x,(y-1)#0f}])'m;r:(,/)y[1]{y#x%y}'m;sl:flip raze (flip y[0 2]){x[0]+`time$((x[1]-x[0])%y)*(0f,'(0.5*1+ randsymm each y#0f|1f&(ffill .db.Ts[.conf.algots]`DICERANDT)^"F"$cfill .db.O1[z;`para][0;`DiceRandT]),'1f)+til y}[;;upid]'m;t:cutoff&sl[1];if[count t;t[0]:y[0;0]];s:raze m#'y[3];(t;x;r;s;sl[0];sl[2])}; /[upid;vl;d;算法],d:(bucket实际开始时间;bucket实际权重;bucket实际结束时间;bucket序号;bucket完整度权重),m:每bucket拆分笔数,x:每笔实际委托量,r:每笔子单时间片历史权重,t:每笔子单计划下单时间,s:bucket序号;slice起始时间;slice结束时间

//algopart:智能拆单算法,x:upid.一般分为两阶段,先生成宏观执行计划,再在定时触发函数里激活单个子任务进行处理(TODO:微观算法优化,在每一时间片内根据盘口动态下单) 

/[upid]时间等分拆单,para:(ExcludeAuctions[9625]:`->无,`1->不参与开盘竞价,`4->不参与收盘竞价,`1 4->全不参加;StartTime[168]->策略生效时间;EndTime[126]->策略结束时间,ParticipationRate[5905]->最大参与率)
TWAP:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];if[not ()~.db.O1[x;`plan];execplan[x];:()];chktext[x];if[.db.O1[x;`typ] in .enum`STOP`STOP_LIMIT;:rejectoa[x;.enum`BROKER_OPTION;`StopOrderUnSupport]];qtyadjust[x;get_last_price[.db.O1[x;`sym]]];fq:0f^.db.O1[x;`sentqty];if[0>=bq:.db.O1[x;`qty]-fq;:()];d:getbuckets[.db.O1[x];1b;`ma];if[0>=n:count d[0];rejectoa[x;.enum`BROKER_OPTION;`HisStat_Missing];:()];vl:roundv[.db.O1[x;`sym`side]] n#bq%n;.db.O1[x;`plan`snap]:(diceplan[x;vl;d;`EQU];(0;fq));execplan[x];}; 

/[upid]VWAP拆单,para:(ExcludeAuctions[9625]:`->无,`1->不参与开盘竞价,`4->不参与收盘竞价,`1 4->全不参加;StartTime[168]->策略生效时间;EndTime[126]->策略结束时间)
VWAP:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];if[not ()~.db.O1[x;`plan];execplan[x];:()];chktext[x];if[.db.O1[x;`typ] in .enum`STOP`STOP_LIMIT;:rejectoa[x;.enum`BROKER_OPTION;`StopOrderUnSupport]];qtyadjust[x;get_last_price[.db.O1[x;`sym]]];fq:0f^.db.O1[x;`sentqty];if[0>=bq:.db.O1[x;`qty]-fq;:()];d:getbuckets[.db.O1[x];0b;.db.Ts[.conf.algots]`VwapModel];if[0>=count d[0];rejectoa[x;.enum`BROKER_OPTION;`HisStat_Missing];:()];vl:{roundv[.db.O1[x;`sym`side]] z*y%sum z}[x;bq;d[1]];if[bq>(sum vl)+getqtymin[.db.O1[x;`sym`side]];rejectoa[x;.enum`BROKER_OPTION;`Algo_VWAP_Fail];:()];.db.O1[x;`plan`snap]:(diceplan[x;vl;d;`EQU];(0;fq));execplan[x];}; 

/[upid],ImplementationShortfall优化算法,para:(StartTime[168]->策略生效时间;EndTime[126]->策略结束时间,MaxChildVol[7412]->单笔委托最大限量,TradingStyle[7420]->激进程度0|1|2),cf:http://www.courant.nyu.edu/~almgren/papers/optliq.pdf(p14)
IS:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];if[not ()~.db.O1[x;`plan];execplan[x];:()];chktext[x];if[.db.O1[x;`typ] in .enum`STOP`STOP_LIMIT;:rejectoa[x;.enum`BROKER_OPTION;`StopOrderUnSupport]];s:.db.O1[x;`sym];if[0=0f^p:getordpx[s;"1";`NEUTRAL];:()];qtyadjust[x;p];fq:0f^.db.O1[x;`sentqty];if[0>=bq:.db.O1[x;`qty]-fq;:()];d:getbuckets[.db.O1[x];0b;`ma];tl:d[0];if[0>=n:count tl;rejectoa[x;.enum`BROKER_OPTION;`HisStat_Missing];:()];tau:(vtimex[fs2e .db.O1[x;`sym];tl[1]]-vtimex[fs2e .db.O1[x;`sym];tl[0]])%04:00:00.000;vT:n*tau;sigma:.temp.HSD[s;`stddev]*p;sp:.temp.HSD[s;`spread]*p;gamma:sp%0.1*.temp.HSD[s;`amt]%p;eps:0.5*sp;eta:10*gamma;lambda:1e-6^(`0`1`2!1e-7 1e-6 1e-5)[`$cfill .db.O1[x;`para][0;`TradingStyle]];kappa:sigma*sqrt lambda% eta;vl:roundv[.db.O1[x;`sym`side]] (bq*2*(.math.sinh 0.5*kappa*tau)%(.math.sinh kappa*vT))*.math.cosh kappa*vT-tau*0.5+ til n;if[bq>(sum vl)+getqtymin[.db.O1[x;`sym`side]];rejectoa[x;.enum`BROKER_OPTION;`Algo_IS_Fail];:()];.db.O1[x;`plan`snap]:(diceplan[x;vl;d;`EQU];(0;fq));execplan[x];};   

/[upid],成交量参与率,para:(StartTime[168]->策略生效时间;EndTime[126]->策略结束时间,ParticipationRate[5905]->参与率,MaxChildVol[7412]->单笔委托最大限量)
VOLPCT:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];if[.db.O1[x;`typ]=.enum`MARKET_ON_CLOSE;.db.O1[x;`algo]:`QMOC;:()];if[not ()~.db.O1[x;`plan];execplan[x];:()];chktext[x];if[0>=0f^"F"$string `$cfill .db.O1[x;`para][0;`ParticipationRate];rejectoa[x;.enum`BROKER_OPTION;`Invalid_ParticipationRate];:()];if[(.db.O1[x;`typ] in .enum`STOP`STOP_LIMIT);if[0>=sp:.db.O1[x;`stoppx];:rejectoa[x;.enum`BROKER_OPTION;`Invalid_StopPx]];if[((1>pxcmp[.db.O1[x;`side];px;sp])&(0<px))|0>=px:.db.QX[.db.O1[x;`sym];`price];:()]];qtyadjust[x;get_last_price[.db.O1[x;`sym]]];fq:0f^.db.O1[x;`sentqty];if[0>=bq:.db.O1[x;`qty]-fq;:()];d:getbuckets[.db.O1[x];1b;`ma];if[0>=n:count d[0];rejectoa[x;.enum`BROKER_OPTION;`HisStat_Missing];:()];vl:$[0<n;roundv[.db.O1[x;`sym`side]] bq,(0|n-1)#0f;()];.db.O1[x;`plan`snap]:(diceplan[x;vl;d;`FIRST];(0;fq));execplan[x];}; 

/[upid]Iceberg算法,para:(StartTime[168]->策略生效时间;EndTime[126]->策略结束时间,DisplaySize[111]->可见单量,ReferencePrice[5691]->参考价格,MaxChildVol[7412]->单笔委托最大限量)
ICEBERG:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];chktext[x];if[.db.O1[x;`typ] in .enum`STOP`STOP_LIMIT;:rejectoa[x;.enum`BROKER_OPTION;`StopOrderUnSupport]];if[0>=p:"F"$string `$cfill .db.O1[x;`para][0;`ReferencePrice];rejectoa[x;.enum`BROKER_OPTION;`Invalid_ReferencePrice];:()];if[0>=ds:roundqty[.db.O1[x;`sym`side]] 0f^"F"$string `$cfill .db.O1[x;`para][0;`DisplaySize];rejectoa[x;.enum`BROKER_OPTION;`Invalid_DisplaySize];:()];fs:.db.O1[x;`sym];sd:.db.O1[x;`side];if[(.db.O1[x;`typ]~.enum`LIMIT)&(0<pxcmp[sd;p;.db.O1[x;`price]]);rejectoa[x;.enum`BROKER_OPTION;`PriceBetterThanReferencePrice];:()];t:`time$now[];if[t<`time$"Z"$cfill .db.O1[x;`para][0;`StartTime];:()];if[t>0Wt^`time$"Z"$cfill .db.O1[x;`para][0;`EndTime];.db.O1[x;`end]:1b;:()];ei:`M^.db.O1[x;`execinst];if[0=0f^px:getordpx[fs;sd;$[ei in `R`T;`LEASTPASSIVE;ei in `P`6;`LEASTAGGRESSIVE;`NEUTRAL]];:()];if[(`6=ei)&0<=pxcmp[sd;p;px];:()];if[.db.O1[x;`sentqty]>.db.O1[x;`cumqty];if[(ei in `R`P)&(0>pxcmp[sd;pl;px])&(0<pl:lastsubordpx[x]);cxloachildren[x]];:()];if[ei in `M`R`P`T;p:$[.db.O1[x;`side]=.enum`BUY;p &;p |] px];qtyadjust[x;p];sq:0f^.db.O1[x;`sentqty];if[.db.O1[x;`cumqty]>.db.O1[x;`qty]-getqtymin[.db.O1[x;`sym`side]];.db.O1[x;`end]:1b;:()];lq:roundqty[.db.O1[x;`sym`side]] .db.O1[x;`qty]-sq;k:.db.QX[.db.O1[x;`sym];`cumqty];if[0>=k;:()];v:roundqty[.db.O1[x;`sym`side]] lq&ds*(1+randsymm 0f|1f&ffill .db.Ts[.conf.algots]`DICERANDQ);if[v>0;j:$[()~.db.O1[x;`snap];0;.db.O1[x;`snap][0]];thre:(getqtymax[fs,sd])&{$[x>0;x;0w]} 0f^"F"$string `$cfill .db.O1[x;`para][0;`MaxChildVol];newsoxfixl[x;`$(string x),"_",(string j),"_",(string newid[]);v;p;j;thre];.db.O1[x;`snap]:(j+1;k;v);];}; /update expiretime:0Np from `O where upid=x,slot=j lfupd[`O;((=;`upid;enlist x);(=;`slot;j));0b;(enlist `expiretime)!enlist 0Np];

/[upid]逼近收盘策略,para:(StartTime[168]->策略生效时间;EndTime[126]->策略结束时间,ParticipationRate[5905]->参考参与率)
QMOC:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];if[not ()~.db.O1[x;`plan];execplan[x];:()];chktext[x];if[(`time$now[])<`time$"Z"$cfill .db.O1[x;`para][0;`StartTime];:()];fq:0f^.db.O1[x;`sentqty];bq:.db.O1[x;`qty]-fq;d:getbuckets[.db.O1[x];0b;`ma];tr:"F"$string `$cfill .db.O1[x;`para][0;`ParticipationRate];if[0>=dv:dvolpred[.db.O1[x;`sym]];:()];k:last 0,where (bq*-1+100%tr)<dv*reverse sums reverse d[1];if[0b;.db.O1[x;`snap]:(`AboutBegin;d[0;k]);:()];d:(k _) each d;vl:{roundv[.db.O1[x;`sym`side]] z*y%sum z}[x;bq;d[1]];.db.O1[x;`plan`snap]:(diceplan[x;vl;d;`EQU];(0;fq));}; 

chktext:{[x]if[0=count cfill pt:.db.O1[x;`para][0;`Text];:()];z:(enlist `TextOrig)!(enlist pt);if[pt~"[safe]";z,:`SliceFreq`DiceFreq`ChildStyle!string .db.Ts[.conf.algots]`SAFESLICEFREQ`SAFEDICEFREQ`SAFESTYLE];.db.O1[x;`para]:enlist (`Text _.db.O1[x;`para][0]),z;};

NONE:DMA:{[x]if[(10#cfill pt:.db.O1[x;`para][0;`Text])~"[strategy]";if[not ()~e:applyparatx[x;10_pt];rejectoa[x;.enum`BROKER_OPTION;`$e,":",pt]];:()];if[(.db.O1[x;`typ] in .enum`STOP`STOP_LIMIT)&(.db.O1[x;`handlinst]<>.enum`MANUAL_ORDER_BEST_EXECUTION);:STOP[x]];DODMA[x];}; /update expiretime:0Np from `O where upid=x,slot=0

DODMA:{[x]$[0=count k:.db.Ax[x;`oid];[k:`$(string x),"_DMA_",(string newid[]);newsofixl[x;k;.db.O1[x;`qty];.db.O1[x;`price];-1];.db.Ax[x;`oid]:k];if[(.db.O[k;`status]=.enum`REJECTED)&(.db.O1[x;`status]<>.enum`REJECTED);rejectoa[x;.enum`BROKER_OPTION;.db.O[k;`text]]]];if[(.db.O[k;`status]=.enum`CANCELED)&(.db.O1[x;`cstatus]=.enum`NULL);cxloasys[x;`NOECXL]];};

STOP:{[x]fs:.db.O1[x;`sym];sd:.db.O1[x;`side];if[0>=sp:.db.O1[x;`stoppx];rejectoa[x;.enum`BROKER_OPTION;`Invalid_StopPx];:()];if[(0>=px)|((1>pxcmp[sd;px;sp])&0<px:.db.QX[fs;`price]);:()];if[0<oq:.db.O1[x;`qty]-0f^.db.O1[x;`sentqty];i:0;newsox[x;(`$(string x),"_",(string i),"_",(string newid[]));oq;getordpx[fs;sd;`MOSTAGGRESSIVE];i;getqtymax[fs,sd]]];if[.db.O1[x;`cumqty]>.db.O1[x;`qty]-getqtymin[fs,sd];.db.O1[x;`end]:1b];}; /止损单

HUNTER:{[x]t:`time$now[];if[t<`time$"Z"$cfill .db.O1[x;`para][0;`StartTime];:()];if[t>0Wt^`time$"Z"$cfill .db.O1[x;`para][0;`EndTime];.db.O1[x;`end]:1b;:()];fs:.db.O1[x;`sym];sd:.db.O1[x;`side];if[0=0f^px:getordpx[fs;sd;`LEASTPASSIVE];:()];ds:roundqty[fs,sd] 0f^"F"$string `$cfill .db.O1[x;`para][0;`DisplaySize];sq:0f^.db.O1[x;`sentqty];if[.db.O1[x;`cumqty]>.db.O1[x;`qty]-getqtymin[.db.O1[x;`sym`side]];.db.O1[x;`end]:1b;:()];lq:roundqty[.db.O1[x;`sym`side]] .db.O1[x;`qty]-sq;v:roundqty[.db.O1[x;`sym`side]] lq&ds*(1+randsymm 0f|1f&ffill .db.Ts[.conf.algots]`DICERANDQ);k1:first sfill .db.Ax[x;`ID1];$[((null k1)|(0>=(-/).db.O[k1;`qty`cumqty]))&(0<v);[k:`$(string x),"_0","_",(string newid[]);k1:newsoxfixl[x;k;v;px;0;getqtymax[fs,sd]];.db.Ax[x;`ID1]:k1];0>pxcmp[sd;.db.O[k1;`price];px];cxlord[k1];()];$[(40000<=sum .db.QX[fs;`bsizeQ])&(t>00:00:06+first tfill .db.Ax[x;`TIME3]);[.db.Ax[x;`TIME3]:t;k:`$(string x),"_2","_",(string newid[]);newsoxfix[x;k;lq&15000f;getordpx[fs;sd;`MOSTAGGRESSIVE];2;getqtymax[fs,sd]]];t>00:00:20+first tfill .db.Ax[x;`TIME2];[.db.Ax[x;`TIME2]:t;k:`$(string x),"_1","_",(string newid[]);newsoxfix[x;k;lq&roundqty[.db.O1[x;`sym`side]] 0.2*sum 2#.db.QX[fs;`bsizeQ];getordpx[fs;sd;`MOSTAGGRESSIVE];1;getqtymax[fs,sd]]];()];}; //`DisplaySize

coverlegs:{[x;y]if[0<sum (-/) each .db.O1[;`sentqty`cumqty] each x;cxloachildren each x;:0b];l1:x[0];l2:x[1];if[(0=q:.db.O1[l1;`cumqty]-.db.O1[l2;`cumqty])|not y;:1b];i:0;k:$[q<0;l1;l2];fss:.db.O1[k;`sym`side];newsoxfixl[k;(`$(string k),"_",(string i),"_",(string newid[]));abs q;getordpx[fss[0];fss[1];`MOSTAGGRESSIVE];i;getqtymax[fss]];0b};

keepxqty:{[x;px;qty;cy;delay]if[0>=px:`float$px;:()];if[(0>pxcmp[.db.O1[x;`side];pl;px])&(0<pl:lastsubordpx[x]);cxloachildren[x];:()];if[(not cy)&(0<wq:.db.O1[x;`sentqty]-.db.O1[x;`cumqty]);:()];if[(`time$now[])<lastsubordtime[x]+delay;:()];if[0<dq:qty-wq;i:0;newsoxfixl[x;(`$(string x),"_",(string i),"_",(string newid[]));dq;px;i;getqtymax[.db.O1[x;`sym`side]]]];}; /[upid;px;qty]保证upid在px排队子单量为qty

PAIRICE:{[x]if[not .db.Ax[x;`full];:()];sids:.db.Ax[x;`sids];if[not .db.Ax[x;`init];{.db.O1[x;`para]:y;}[;.db.O1[x;`para]] each sids;.db.Ax[x;`llag`slag`init]:(sids[0];sids[1];1b)];ll:.db.Ax[x;`llag];sl:.db.Ax[x;`slag];fs:.db.O1[ll;`sym];ds:0f^"F"$string `$cfill .db.O1[x;`para][0;`DisplaySize];cy:"B"$string `$cfill .db.O1[x;`para][0;`CycleOrder];dq:$["B"$string `$cfill .db.O1[x;`para][0;`DynHedge];.db.O1[sl;`sentqty]-.db.O1[ll;`sentqty];0f];mnp:0w^"F"$string `$cfill .db.O1[x;`para][0;`MaxNetPos];np:.db.O1[ll;`cumqty]-.db.O1[sl;`cumqty];delay:"T"$string `$cfill .db.O1[x;`para][0;`OrderDelay];$[14:58:00>=t:`time$now[];[keepxqty[ll;$[np>=mnp;first 1_.db.QX[fs;`bidQ];.db.QX[fs;`bid]];ds|dq;cy;delay];keepxqty[sl;$[(neg np)>=mnp;first 1_.db.QX[fs;`askQ];.db.QX[fs;`ask]];ds|neg dq;cy;delay]];if[coverlegs[sids;"B"$string `$cfill .db.O1[x;`para][0;`DayendCover]];.db.O1[x;`end]:1b]];}; /

PORTMOVE:{[x]if[not .db.Ax[x;`full];:()];sids:.db.Ax[x;`sids];if[not .db.Ax[x;`init];{.db.O1[x;`para]:y;}[;.db.O1[x;`para]] each sids;.db.Ax[x;`init]:1b];cl:sids[0];ol:sids[1];cs:.db.O1[cl;`sym];os:.db.O1[ol;`sym];sp:.db.QX[os;`price]-.db.QX[cs;`price];if[((sp>su)&(0<su:0f^"F"$string `$cfill .db.O1[x;`para][0;`SpreadSup]))|((sp<si)&(0<si:0f^"F"$string `$cfill .db.O1[x;`para][0;`SpreadInf]));:()];TWAP each sids;}; /`SpreadSup`SpreadInf

PORTVWAP:{[x]if[not .db.Ax[x;`full];:()];sids:.db.Ax[x;`sids];if[0=count sids:sids where not .db.O1[;`end] each sids;.db.O1[x;`end]:1b;:()];if[not .db.Ax[x;`init];{.db.O1[x;`para]:y;}[;.db.O1[x;`para]] each sids;.db.Ax[x;`init]:1b];VWAP each sids;};

PORTTWAP:{[x]if[not .db.Ax[x;`full];:()];sids:.db.Ax[x;`sids];if[0=count sids:sids where not .db.O1[;`end] each sids;.db.O1[x;`end]:1b;:()];if[not .db.Ax[x;`init];{.db.O1[x;`para]:y;}[;.db.O1[x;`para]] each sids;.db.Ax[x;`init]:1b];TWAP each sids;};

PORTIS:{[x]if[not .db.Ax[x;`full];:()];y:.db.Ax[x;`sids];if[0=count y:y where not .db.O1[;`end] each y;.db.O1[x;`end]:1b;:()];if[.db.Ax[x;`init];:execplan each y];{.db.O1[x;`para]:y;}[;.db.O1[x;`para]] each y;vL:flip .db.O1[;`sym`qty`side] each y;vFS:vL[0];vLambda:1e-6^(`0`1`2!1e-7 1e-6 1e-5)[`$cfill .db.O1[x;`para][0;`TradingStyle]];k:(.temp.HCOV`syms)?vFS;if[any 0=0f^vP:getordpx[;"1";`NEUTRAL] each vFS;:()];reta:(1e-2*(%/)flip .temp.HSD[;`amt`spread] each vFS)%vP*vP;vP:?[vL[2]=.enum`BUY;1;-1]*vP;vA:vLambda*reta*vP*((.temp.HCOV`covm)[k;k])$.math.diag(vP);vE:.qml.mev vA;vM:count where 0<vE[0];vKappa:sqrt vM#vE[0];vRU:.qml.mpinv vU:flip vM#vE[1];vY:vRU$flip enlist vL[1];d:(')[getbuckets[;0b;`ma];.db.O1] y i:first idesc `XSHE=vEX:fs2e each vFS;ve:vEX[i];n:count tl:d[0];vT:n*(vtimex[ve;tl[1]]-vtimex[ve;tl[0]])%04:00:00.000;t:vT*.math.steps[1 0f;n];vQ:(.db.O1[;`sym`side]each y) roundv' {nonneg neg 1_ deltas raze x} each flip vU$/:(0f^.math.sinh[vKappa*/:vT*.math.steps[1 0f;n]]%\:.math.sinh vKappa*vT)*\:vY;y {[d;x;y].db.O1[x;`plan`snap]:(diceplan[x;y;d;`EQU];(0;0f));}[d]' vQ;.db.Ax[x;`init]:1b;}; /eta=spread%0.01*ADV=spread%0.01*AMT


list2port:{[x;y]if[null z:`$lid:cfill .db.O1[x;`para][0;`ListID];:()];he:.db.O1[x;`clt];k:exec first id from .db.O1 where clt=he,cltid=z;n:"I"$cfill .db.O1[x;`para][0;`ListSize];if[null k;k:newid[];.db.O1[k;`status`extype`slot`clt`cltid`cltalt`algo`para]:(.enum`NEW;`LIST;n;he;z;`LIST2PORT;y;.db.O1[x;`para]);.db.Ax[k]:`full`sids`init`seq!(0b;();1b;0i)];if[.db.Ax[k;`seq]>=.db.O1[k;`slot];$[n<=.db.O1[k;`slot];:rejectoa[x;.enum`BROKER_OPTION;`$"ListAlreadyFull:",lid];[alertoa[x;.enum`INFO;.enum`USERATTENTIONREQ;`$"ListAppendNewLeg:",lid];.db.O1[k;`slot]:n]]];se:.db.Ax[k;`seq]+1i;.db.O1[x;`extype`upid`slot`algo]:(`LISTCHILD;k;se;`);.db.Ax[k;`sids`seq`full]:(.db.Ax[k;`sids],x;se;se>=.db.O1[k;`slot]);}; 

LISTTWAP:list2port[;`PORTTWAP];LISTVWAP:list2port[;`PORTVWAP];LISTIS:list2port[;`PORTIS];PAIRMOVE:list2port[;`PORTMOVE];

//PORTIS:{[x]if[not Ax[x;`full];:()];y:Ax[x;`sids];if[0=count y:y where not .db.O1[;`end] each y;lsetk[`.db.O1;(x;`end);1b];:()];if[Ax[x;`init];:execplan each y];{lsetk[`.db.O1;(x;`para);y];}[;.db.O1[x;`para]] each y;vL:flip .db.O1[;`sym`qty`side] each y;vFS:vL[0];vLambda:1e-6^(`0`1`2!1e-7 1e-6 1e-5)[`$cfill .db.O1[x;`para][0;`TradingStyle]];k:(HCOV`syms)?vFS;if[any 0=0f^vP:getordpx[;"1";`NEUTRAL] each vFS;:()];reta:(1e-2*(%/)flip HSD[;`amt`spread] each vFS)%vP*vP;vP:?[vL[2]=.enum`BUY;1;-1]*vP;vA:vLambda*reta*vP*((HCOV`covm)[k;k])$diag(vP);vE:.qml.mev vA;vKappa:sqrt 0f|vE[0];vRU:.qml.mpinv vU:flip vE[1];vY:vRU$flip enlist vL[1];d:(')[getbuckets[;0b;`ma];.db.O1] y i:first idesc `XSHE=vEX:fs2e each vFS;ve:vEX[i];n:count tl:d[0];tau:(vtimex[ve;tl[1]]-vtimex[ve;tl[0]])%04:00:00.000;vT:n*tau;t:vT*steps[1 0f;n];vQ:(.db.O1[;`sym`side]each y) roundv' {nonneg neg 1_ deltas raze x} each flip vU$/:(0f^sinh[vKappa*/:vT*steps[1 0f;n]]%\:sinh vKappa*vT)*\:vY;y {[d;x;y]lsetk[`.db.O1;(x;`plan`snap);(diceplan[.db.O1[x;`sym`side];y;d;`EQU];(0;0f))];}[d]' vQ;lsetk[`Ax;(x;`init);1b];}; /eta=spread%0.01*ADV=spread%0.01*AMT

//PORTIS:{[x]if[not Ax[x;`full];:()];y:Ax[x;`sids];if[0=count y:y where not .db.O1[;`end] each y;lsetk[`.db.O1;(x;`end);1b];:()];if[Ax[x;`init];:execplan each y];{lsetk[`.db.O1;(x;`para);y];}[;.db.O1[x;`para]] each y;vL:flip .db.O1[;`sym`qty] each y;vFS:vL[0];vX:vL[1];lambda:1e-6^(`0`1`2!1e-7 1e-6 1e-5)[`$cfill .db.O1[x;`para][0;`TradingStyle]];k:(HCOV`syms)?vFS;reta:1e-2*(%/)flip HSD[;`amt`spread] each vFS;vA:lambda*reta*(HCOV`covm)[k;k];vE:.qml.mev vA;kappa:sqrt vE[0];vRU:inv vU:flip vE[1];vY:vRU$flip enlist vX;d:(')[getbuckets[;0b;`ma];.db.O1] y i:first idesc `XSHE=vEX:fs2e each vFS;ve:vEX[i];n:count tl:d[0];tau:(vtimex[ve;tl[1]]-vtimex[ve;tl[0]])%04:00:00.000;vT:n*tau;t:vT*steps[1 0f;n];vQ:(.db.O1[;`sym`side]each y) roundv' {nonneg neg 1_ deltas raze x} each flip vU$/:(sinh[kappa*/:vT*steps[1 0f;n]]%\:sinh kappa*vT)*\:vY;y {[d;x;y]lsetk[`.db.O1;(x;`plan`snap);(diceplan[.db.O1[x;`sym`side];y;d;`EQU];(0;0f))];}[d]' vQ;lsetk[`Ax;(x;`init);1b];}; 

TESTALGO:{[x]fs:.db.O1[x;`sym];si:.db.O1[x;`side];q:.db.QX[fs];h:(!/)2 0N#raze q[$[si=.enum`BUY;`askQ`asizeQ;`bidQ`bsizeQ]];k:where h>=100f;if[0=count k;:()];pm:max k;cq:sum k#h;};

/
SLICE:{[x]if[0=0f^p:getordpx[.db.O1[x;`sym];.db.O1[x;`side];.db.O1[x;`style]];:()];thre:(getqtymax[.db.O1[x;`sym`side]])&{$[x>0;x;0w]} 0f^"F"$string `$cfill .db.O1[x;`para][0;`MaxChildVol];$[()~.db.O1[x;`plan];[bq:.db.O1[x;`qty]-0f^.db.O1[x;`sentqty];n:1|ceiling bq%thre;vl:roundv n#bq%n;v0:first vl;.db.O1[x;`plan`snap]:((enlist vl);(0;v0));newsox[x;`$(string x),"_0","_",(string newid[]);v0;p;0;thre]];[j:.db.O1[x;`snap][0];v:.db.O1[x;`snap][1];if[0f=exec sum cancelqty from O where upid=x,slot=j;$[j=-1+count .db.O1[x;`plan][0];.db.O1[x;`end]:1b;[j:j+1;v:.db.O1[x;`plan][0][j];.db.O1[x;`snap]:(j;v);newsox[x;`$(string x),"_",(string j),"_",(string newid[]);v;p;j;thre]]]]]];}; //[upid]para:(MaxChildVol[7412]->单笔委托最大限量),小量拆单,一笔单成交后报下一笔单

getbuckets0:{[x;y;z]ap:`$cfill .db.O1[x;`para][0;`ExcludeAuctions];st:"Z"$cfill .db.O1[x;`para][0;`StartTime];et:"Z"$cfill .db.O1[x;`para][0;`EndTime];s:.db.O1[x;`sym];m:.db.Ts[.conf.algots]`SLICEFREQ;api:$[y|ap=`$"1 4";();ap=`1;enlist 0W;ap=`4;enlist 0;0 0W];$[`ls=z;[d:select first bucketstart,last bucketstop,tr:sum trls by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:trls from HSVP where sym=s,bucket in api];`arma=z;[d:select first bucketstart,last bucketstop,tr:sum trarma by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:trarma from HSVP where sym=s,bucket in api];[d:select first bucketstart,last bucketstop,sum tr by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr from HSVP where sym=s,bucket in api]];st:$[null st;-0Wt;`time$st]|`time$.db.O1[x;`ntime];et:$[null et;0Wt;`time$et];d:value exec bucketstart,tr,bucketstop,seq from `bucketstart xasc d where bucketstart>=st,bucketstart<=et};

//diceplan0:{[x;y;z]cutoff:(last y[2])-.db.Ts[.conf.algots]`CUTOFFSET;m:.db.Ts[.conf.algots]`DICEFREQ;x:(,/)$[`EQU=z;(roundv m#)each x%m;{roundv y,(x-1)#0f}[m] each x];r:(,/)(m#)each y[1]%m;t:cutoff&(,/)y[0]({y+z*til x}[m])'(`time$(`int$y[2]-y[0])%m);(t;x;r)};


//非整数分钟处理(TODO)
getbuckets1:{[x;y;z]ap:`$cfill .db.O1[x;`para][0;`ExcludeAuctions];st:"Z"$cfill .db.O1[x;`para][0;`StartTime];et:"Z"$cfill .db.O1[x;`para][0;`EndTime];s:.db.O1[x;`sym];m:.db.Ts[.conf.algots]`SLICEFREQ;api:$[y|ap=`$"1 4";();ap=`1;enlist 0W;ap=`4;enlist 0;0 0W];$[`eq=z;[d:select first bucketstart,last bucketstop,tr:m*1f by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:1f from HSVP where sym=s,bucket in api];`ls=z;[d:select first bucketstart,last bucketstop,tr:sum trls by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:trls from HSVP where sym=s,bucket in api];`arma=z;[d:select first bucketstart,last bucketstop,tr:sum trarma by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr:trarma from HSVP where sym=s,bucket in api];[d:select first bucketstart,last bucketstop,sum tr by seq:1+m xbar i from select from HSVP where sym=s,bucket>0,bucket<0W;d,:select seq:bucket,bucketstart,bucketstop,tr from HSVP where sym=s,bucket in api]];st:$[null st;-0Wt;`time$st]|`time$.db.O1[x;`ntime];et:$[null et;0Wt;`time$et];d:value exec bucketstart,tr,bucketstop,seq from update tr:tr%sum tr from update tr:?[(seq=0)|(seq=0W);1f;(1+bucketstop-bucketstart)%`time$m*00:01]*tr from update bucketstart:st|bucketstart,bucketstop:et&bucketstop from select from `bucketstart xasc d where bucketstop>=st,bucketstart<=et};

diceplan1:{[x;y;z]cutoff:(last y[2])-.db.Ts[.conf.algots]`CUTOFFSET;m:.db.Ts[.conf.algots]`DICEFREQ;x:(,/)$[`EQU=z;(roundv m#)each x%m;{roundv y,(x-1)#0f}[m] each x];r:(,/)(m#)each y[1]%m;t:cutoff&(,/)y[0]({y+z*til x}[m])'(`time$(`int$y[2]-y[0])%m);(t;x;r)};

PAIRICE:{[x]if[not Ax[x;`full];:()];sids:Ax[x;`sids];if[0=count sids:sids where not .db.O1[;`end] each sids;lsetk[`.db.O1;(x;`end);1b];:()];if[not Ax[x;`init];{lsetk[`.db.O1;(x;`para);update ReferencePrice:?[.db.O1[x;`side]=.enum`BUY;ReferencePriceL;ReferencePriceS] from y];}[;.db.O1[x;`para]] each sids;lsetk[`Ax;(x;`init);1b]];$[14:55:00>=t:`time$now[];ICEBERG each sids;0<sum (-/) each .db.O1[;`sentqty`cumqty] each sids;cxloachildren each sids;if[coverlegs[sids];lsetk[`.db.O1;(x;`end);1b]]];};

DMA1:{[x]if[.db.O1[x;`handlinst]=.enum`MANUAL_ORDER_BEST_EXECUTION;:MANUAL[x]];if[.db.O1[x;`sentqty]>.db.O1[x;`cumqty];:()];p:.db.O1[x;`price];qtyadjust[x;p];fss:.db.O1[x;`sym`side];if[0<oq:.db.O1[x;`qty]-0f^.db.O1[x;`sentqty];newsoxfix[x;`$(string x),"_DMA","_",(string newid[]);oq;p;0;getqtymax[fss]];;if[.db.O1[x;`cumqty]>.db.O1[x;`qty]-getqtymin[fss];.db.O1[x;`end]:1b]];};